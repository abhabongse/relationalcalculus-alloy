start: tables_def queries
tables_def: "tables" "{" (table ","?)* "}"
table: TABLE_NAME "(" fields ")"
fields: (FIELD_NAME ("," FIELD_NAME)* ","?)?

queries: "queries" "{" (query_stmt ","?)* "}"
query_stmt: QUERY_IDENTIFIER "=" "{" tuple_expr ":" iff_test "}"
tuple_expr: FIELD_NAME ("," FIELD_NAME)* ","?

?iff_test: implies_test (_IFF_OP implies_test)?
?implies_test: or_test (_IMPLIES_OP or_test)?
?or_test: and_test (_OR_OP and_test)*
?and_test: not_test (_AND_OP not_test)*
?not_test: _NOT_OP atom_test  -> not
         | atom_test
?atom_test: "(" iff_test ")"
          | table 
          | FIELD_NAME COMP_OP FIELD_NAME  -> compare_op
          | _FOR_ALL_OP "[" FIELD_NAME "]" "(" or_test ")"  -> for_all
          | _THERE_EXISTS_OP "[" FIELD_NAME "]" "(" or_test ")" -> there_exists
         
TABLE_NAME: /[A-Z][A-Za-z0-9_]*/
FIELD_NAME: /[a-z][A-Za-z0-9_]*/
QUERY_IDENTIFIER: /\$[A-Za-z0-9_]+/

_IFF_OP: "<=>" | "⇔" | "↔" | "IFF"
_IMPLIES_OP: "=>" | "⇒" | "→" | "IMPLIES"
_OR_OP: "∨" | "|" | "OR"
_AND_OP: "∧" | "&" | "AND"
_NOT_OP: "~" | "¬" | "NOT"
_FOR_ALL_OP: "∀" | "ALL"
_THERE_EXISTS_OP: "∃" | "EXISTS"

COMP_OP: "==" | "!=" | ">=" | ">" | "<=" | "<"

%import common.WS
%ignore WS
